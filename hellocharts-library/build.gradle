plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'ir.mahdiparastesh.hellocharts'
    compileSdk Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion '34.0.0 rc3'

    defaultConfig {
        minSdk Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdk Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing { singleVariant("release") {} }
}

dependencies {
    implementation 'androidx.core:core:1.10.0'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

afterEvaluate { // You only need to set the access token and execute "gradle publish"...
    publishing {
        repositories {
            maven {
                url 'https://maven.pkg.github.com/fulcrum6378/hellocharts'
                credentials {
                    username 'fulcrum6378'
                    password 'ghp_XXXXX'
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'ir.mahdiparastesh'
                artifactId 'hellocharts'
                version project.VERSION_NAME
            }
        }
    }
}
